Nix Syntax Primer
=================

== Intro

Nix is the name shared by both the https://nixos.org/nix/manual[Nix package manager] and its https://nixos.org/nix/manual#ch-expression-language[Nix expression language]. The expression language is used extensively in working with Nix-the-package-manager and everything built on top of it, including for configuring the https://nixos.org/nixos/manual[NixOS] Linux distribution. This is a primer on the Nix expression language.

My hope is that with this guide -- reading it, skimming it, or just keeping it handy if you get stuck -- you can get your bearings enough to read through existing Nix code and, if you're so inclined, start writing some of your own without fear.

I assume no prior familiarity with Nix, so you can start here from scratch. Familiarity with functional programming or a lazily evaluated language might help, but neither are required.

=== Other similar primers

- https://medium.com/@MrJamesFisher/nix-by-example-a0063a1a4c55
- https://learnxinyminutes.com/docs/nix/
- https://nixcloud.io/tour

== Basics

=== What to expect

Before getting to details, here's some orientation.

- Nix expression values are **immutable**.
- Nix expressions have **no side effects**.
- Nix expressions are **lazily evaluated**.
- Nix expression values are **dynamically typed**.
- Nix expressions require **no compilation**.
- Nix expression syntax is **insensitive to whitespace**.

Nix is fundamentally a _configuration language_, not a general-purpose programming language. You can't use it (alone) to write a web server, or a kernel extension, or a Chrome extension, or an Android app. I have come to think of Nix as more like a "programmable JSON" than anything, where by "programmable" I mean proper _lambda calculus_, not some half-measure like http://docs.ansible.com/ansible/[YAML with embedded string templates] or https://docs.mongodb.com/manual/meta/aggregation-quick-reference/[a part-JSON, part-SQL exquisite corpse].

The purpose of the Nix language is to _deterministically create files and directories and manage their interdependencies_. If you have a Nix expression for a software package, you want it to build the same exact package every time, in all contexts, even if you have other versions of the same package already installed, even if you build it on a totally different computer, and even if you walk away from it and come back to it in a year.

To that end, the Nix language itself is geared to be deterministic. Immutability and the lack of side effects help make it very difficult to write non-deterministic expressions. There _are_ a few pieces of context a nix expression can choose to be sensitive to -- environment variables, contents of other local files, platform of current computer -- but those are only used when necessary. On top of the immutable, side effect-free foundation, lazy evaluation lends flexibility and efficiency, especially with large expressions like the nixpkgs package set.

=== JSON types

Nix has most of the same primitive value types as JSON, namely: numbers, booleans, strings, and `null`. One notable exception is that Nix does not have floats, so all numbers are integers.

[options="header",cols="1,2,2"]
|====
|        |Nix     |JSON
|Number  |`1`     |`1`
|Float   |_n/a_   |`3.14`
|Boolean |`true`  |`true`
|String  |`"str"` |`"str"`
|Null    |`null`  |`null`
|====

Also like JSON, Nix has lists and objects, albeit with slightly different syntax. Note that what JSON calls "objects", Nix calls "attribute sets" or sometimes just "sets".

[options="header",cols="1,2,2"]
|====
|           |Nix                            |JSON
|List       |`[1 2 3]`                      |`[1, 2, 3]`
|Object/Set |`{foo = "bar"; baz = "quux";}` |`{"foo": "bar", "baz": "quux"}`
|====

Beyond these, Nix also has _**function**_, _**path**_, and _**derivation**_ values and a few other sorts of expressions for writing actual programs.

We'll cover all these below except for derivations, because derivations deserve their own primer.

=== Running the examples

The `#` character begins line comments, so I will use `#->` in examples to denote what an expression evaluates to. I will write standalone examples whenever possible so they can be easily copied and run from your REPL.

There are a few ways to interactively evaluate Nix expressions. I recommend following along with the examples using one of these.

- https://nixcloud.io/tour
+
This is its own beginner's tour of the Nix language, but it includes an editor that can evaluate expressions Nix right on the page. I recommend this if you haven't installed Nix on your computer or if you don't have a terminal handy.

- `nix-repl`
+
If you have already installed Nix on your computer, you may already know that you can install `nix-repl` with `nix-env --install nix-repl`. It's nothing fancy, but it's better than no REPL. Use `:?` for help with commands. One unfortunate caveat is `nix-repl` doesn't support multiline expressions, though sometimes it's still worth it to explore unfamiliar territory with its tab completion.

- `nix-instantiate`
+
If you already have Nix installed, you may also evaluate individual expressions with `nix-instantiate --eval --strict --expr '...'` or files with `nix-instantiate --eval --strict path/to/file.nix`. A Nix file just contains one big expression. If you want to paste examples verbatim into your terminal, something like  the following should work fine.
+
[source,sh]
pbpaste | tee /dev/stderr | nix-instantiate --eval --strict -
+
(This is how I've checked my examples.)

NOTE: Since Nix expressions are whitespace-insensitive, the formatting of examples is all just my own preference. I try at least to be consistent throughout, and to use indentation in ways that are least misleading.

== Expression forms

=== Strings

Concatenate with `+`.

[source,nix]
"Hello " + "world"
#-> "Hello world"

Nix has string interpolation with `${...}`, though in Nix land it usually goes by the less familiar name of "antiquotation". You can put any expression in those braces, including other strings.

[source,nix]
let
  name = "world";
in
  "Hello ${name + "!"}"
#-> "Hello world!"

You can also write strings inside double single-quotes, `''like this''`. This form allows multiline strings, and it will intelligently strip indentation!

[source,nix]
let
  name = "world";
in
  ''
  # This can be any text!
  echo "Hello ${name}"
  ''
#-> "# This can be any text!\necho \"Hello world\"\n"

NOTE: Once you start using this, you'll wish every language had multiline strings that were so pleasant. I know I do!

It's common in the nixpkgs repo for bash code snippets and other config files to be written in strings this way.

=== Integers

Integers in Nix are as you might expect.

[source,nix]
2 * 4 - 8 / (5 - 1)
#-> 6

Integers are 64 or 32 bits, depending on your system.

[source,nix]
9223372036854775807 + 1
#-> -9223372036854775808

Because Nix has no floating point numbers, all division is integer division.

[source,nix]
5 / 3
#-> 1

Just don't write a division without spaces.

[source,nix]
2/1
#-> /Users/ryanartecona/blog/2/1

What? This happens because `/` without surrounding spaces gets interpreted as a path separator. `2/1` gets interpreted as a relative path instead of arithmetic, and that path gets expanded to an absolute path automatically. More about <<_paths,Paths>> below.


=== Booleans

The boolean operators include the usual suspects.

[options="header",cols="1,2"]
|====
| Operation | Expression
| Conjunction a|
[source,nix]
true && false
#-> false

| Disjunction a|
[source,nix]
----
true \|\| false
#-> true
----

| Negation a|
[source,nix]
!false
#-> true

| Implication a|
[source,nix]
true -> false
#-> false
|====

The one boolean operator you may be less familiar with is `->` for implication.

You can read `p -> q` as "_p_ implies _q_". This means if _p_ is true, then _q_ must be true. It's logically equivalent to `!p || q`, if that helps.

The `->` operator is usually only used to assert mutually consistent configuration options. Imagine you write a package which allows setting two feature flags, `includeGUI` and `useFancyGUITheme`. If it only makes sense for `useFancyGUITheme` to be true if `includeGUI` is _also_ true, then you might write `assert useFancyGUITheme -> includeGUI;` to ensure your package won't try to include a GUI theme without including the GUI.

All boolean operators in Nix expect proper boolean values. They won't coerce non-boolean values to booleans, so there's no notion of "truthy" or "falsy" values like in some languages.

[source,nix]
true && null
#-> error: value is null while a Boolean was expected, at (string):1:1

==== Conditionals

You can write a conditional expression with `if ... then ... else ...`.

[source,nix]
if true
  then "it was true"
  else "it was false"
#-> "it was true"

Just remember that this is an `if` _expression_, not an `if` _statement_. It evaluates to either the expression in the `then` branch or the expression in the `else` branch, but it's not "executed". Since there's also no mutability or side effects in the Nix language, there would be little point to an `if ... then ...` without an `else` branch, so you must always give both the `then` branch and the `else` branch.

There's no special syntax for chaining conditionals (no `elif` or `elsif`), but because they're just ordinary expressions, you can nest them however you like. You might also like to keep indentation to a minimum with a little clever formatting.

[source,nix]
let
  x = 1;
in
  if x < 0 then
    "negative"
  else if x > 0 then
    "positive"
  else
    "zero"
#-> "positive"

==== Equality

Test for equality with `==`.

[source,nix]
1 == 1
#-> true

You can test for equality with values of different type, and the test will just evaluate to `false`.

[source,nix]
true == "true"
#-> false

Two expressions are equal if they evaluate to the same value. Lists and attribute sets are equal if and only if all their elements are equal.

[source,nix]
----
[1 2] == [1 2]
#-> true
----

[source,nix]
{ x = "x"; } == { x = "x"; }
#-> true

Perhaps surprisingly, functions always test as unequal to all other functions, even themselves.

[source,nix]
let
  f = (x: x);
in
  f == f
#-> false

This also means testing two lists or attribute sets for equality, if _either one_ of them contains any functions, will _always_ return `false`, even if they are otherwise equal.

[source,nix]
{a = 1; f = (x: x);} == {a = 1; f = (x: x);}
#-> false

=== Lists

Lists in Nix are written in square brackets with just whitespace separating elements.

[source,nix]
----
[ 1 true null ]
#-> [ 1 true null ]
----

Concatenate lists with `++`.

[source,nix]
----
[ 1 2 ] ++ [ 3 4 ]
#-> [ 1 2 3 4 ]
----

There's no special syntax for indexing into a list. If you need to, you can use `builtins.elemAt`.

[source,nix]
builtins.elemAt [1 2 3] 1
#-> 2

NOTE: The `builtins` here is, well, an attribute set full of inbuilt functions. See <<_builtins,builtins>>.

=== Attribute sets

An attribute set is like JSON's "Object", Ruby's "Hash", Python's "dict", etc., but with an obscure name. It's a mapping from names to values.

[source,nix]
----
let
  x = { key = "value"; };
in
  x.key
#-> "value"
----

An attribute name must be a string, but it can be any string. If it doesn't happen to be a valid identifier (i.e. it includes spaces or special characters), you can write the name as a string literal with quotes. The value, of course, can be expression.

[source,nix]
let
  x = { "my key" = 1 + 2; };
in
  x."my key"
#-> 3

You can use string interpolation in attribute names, if you need to, both while defining and accessing.

[source,nix]
let
  name = "key";
  x = {
    "${name} 1" = "value #1";
    "key 2" = "value #2";
  };
in
  x."${name} 2"
#-> "value #2"

You can also merge the attributes of two sets into one with `//`. The right hand side wins if attribute names conflict, and the merge is shallow.

[source,nix]
let
  foo = { x = 1; z = { a = "a"; }; };
  bar = { y = 3; z = { b = "b"; }; };
in
  (foo // bar)
#-> { x = 1; y = 3; z = { b = "b"; }; }

==== Attribute membership

If you try to access a nonexistent attribute on a set, you get an error.

[source,nix]
{ x = 1; }.y
#-> error: attribute ‘y’ missing, at (string):1:1

You can use `?` to check if a set has a certain key before trying to access it.

[source,nix]
{ x = 1; } ? y
#-> false

==== Recursive sets

Usually, when you define a set, the attributes cannot refer to each other.

[source,nix]
{
  name = "hello-${version}";
  version = "1.0";
}
#-> error: undefined variable ‘version’ at (string):2:19

You can always get around this by using a `let` binding, but often it's more convenient to define a "recursive" set instead, which lets attributes refer to each other. You do this with the `rec` keyword.

[source,nix]
rec {
  name = "hello-${version}";
  version = "1.0";
}
#-> { name = "hello-1.0"; version = "1.0"; }

Once defined, a normal attribute set and a recursive attribute set behave exactly the same. Whether a set is recursive or not affects how you can write it, but it doesn't change how you can use it. It's just syntactic sugar.

In particular, it's _not_ the case that changing the value of one attribute in a recursive set will automatically change the value of other attributes that used it in their definition, even though it might look like it!

[source,nix]
let
  hello1 = rec {
    name = "hello-${version}";
    version = "1.0";
  };
  v2 = {
    version = "2.0";
  };
in
  (hello1 // v2)
#-> { name = "hello-1.0"; version = "2.0"; }

==== Inheriting

If you want to turn a bound name into an attribute in a set, you can use `inherit ...;`.

[source,nix]
let
  x = 1;
  y = 2;
  z = 3;
in
  {
    x = x;
    inherit y z;
  }
#-> { x = 1; y = 2; z = 3; }

NOTE: This is in a way the opposite of <<_literal_with_literal_expression,`with` expressions>>, which turn attributes of a set into bound names.

This feature is similar to "field punning" in some languages or "property value shorthand" in ES6, so it may feel familiar if you can see past the `inherit` keyword.

If want to cherry pick specific attributes in one set to include in another set, you may use an alternate form of `inherit`.

[source,nix]
let
  foo = {x = 1; y = 2;};
in
  { inherit (foo) x y; }
#-> { x = 1; y = 2; }

The parens around `(foo)` are necessary here, as they change the meaning of `inherit`! It's unfortunate syntax, but important to know.

==== Nested definitions

Sometimes when writing Nix code you have to work with deeply nested attribute sets, such as when writing a configuration file for a NixOS machine.

Nix provides a shorthand for defining nested attribute sets which can make them easier to read and write.

[source,nix]
{
  explicit = {
    a = 1;
    b = {
      c = 2;
    };
  };
  short.a = 1;
  short.b.c = 2;
}
#-> { explicit = { a = 1; b = { c = 2; }; }; short = { a = 1; b = { c = 2; }; }; }

Just know that you can't mix and match these styles in an ambiguous way. Each key, at any level, can use one or the other style but not both.

[source,nix]
{
  foo.a = 1;
  foo = {
    b = 2;
  };
}
#-> error: attribute ‘foo’ at (string):3:3 already defined at (string):2:3

=== Bindings and scopes

==== `let` binding

If you haven't already guessed, you can bind a name to a value with `let ... = ...; in ...`.

[source,nix]
----
let x = "value"; in {key = x;}
#-> { key = "value"; }
----

Note that these are constant bindings to immutable values. You can reuse a bound name in an inner scope ("shadowing" it), but you can't reassign it or otherwise change a value once it's assigned. These aren't what most languages call "variables".

You can bind multiple names in a single `let` expression.

[source,nix]
----
let
  x = 1;
  y = 2;
in
  x + y
#-> 3
----

`let` bindings are recursive, so names on the same level can refer to each other just like names in recursive sets.

[source,nix]
let
  name = "hello-${version}";
  version = "1.0";
in
  name
#-> "hello-1.0"

And, of course, a `let` binding is just another type of expression, so you can nest them.

[source,nix]
----
let
  first = (
    let
      second = "Hello";
    in
      second + " "
  );
in
  let
    third = "world";
  in
    first + third
#-> "Hello world"
----

==== `with` expression

A `with` expression brings all the attributes of a set into scope. It exists purely for convenience, as it's often much more pleasant than using fully qualified names or lots of `let` bindings instead.

[cols="1,2"]
|====
| qualified names a|
[source,nix]
----
let
  pkgs = import <nixpkgs> {};
in
  [pkgs.foo pkgs.bar pkgs.baz]
----

| local bindings a|
[source,nix]
----
let
  pkgs = import <nixpkgs> {};
  foo = pkgs.foo;
  bar = pkgs.bar;
  baz = pkgs.baz;
in
  [foo bar baz]
----

| `with` expression a|
[source,nix]
----
let
  pkgs = import <nixpkgs> {};
in
  with pkgs;
  [foo bar baz]
----

|====

NOTE: All 3 of these are different ways of writing the same thing.

The one big caveat with `with` expressions is that a name bound by `with` _cannot shadow_ a name that was already bound. Another way to think about this is that _explicit_ `let` bindings take precedence over _implicit_ `with` bindings, _even if_ the `with` comes after.

[source,nix]
----
let
  item = "we want this";
  suspiciousSet = {item = "we don't want this";};
in
  with suspiciousSet; item
#-> "we want this"
----

This lets you use `with` on a set for convenience without fear that it might clobber your local bindings and break your code.

=== Functions

Basic function syntax is quite terse: you have an argument on the left and a function body on the right separated only by a `:`. It's sometimes easy to miss if you're not looking for it.

Here is a function which simply adds `1` to its argument.

[source,nix]
----
x: x + 1
#-> <LAMBDA>
----

Nix calls functions "lambdas". There's no distinction, and I only use "function" to avoid negative connotations from some languages where lambdas are second class. More importantly, functions in Nix aren't strings in disguise like in _some_ silly languages, so if you try to print one, Nix just says `<LAMBDA>` (or sometimes `«lambda»`).

To apply a function to an argument, simply place the argument after the function.

[source,nix]
----
let
  add1 = (x: x + 1);
in
  add1 2
#-> 3
----

NOTE: The `add1 2` here could be called a "function call" or, equivalently, a "function application". Nix calls them "applications", so I will too. Just know that there's no difference.

Be careful to to use parentheses with function applications inside list literals, or else the function and arguments will become individual elements instead.

[source,nix]
let
  add1 = (x: x + 1);
in
  [
    1
    add1 2
    (add1 2)
  ]
#-> [ 1 <LAMBDA> 2 3 ]

==== Currying

All functions in Nix take exactly 1 argument (they're "unary"). Technically speaking, you can't define a function which takes more than 1 argument.

But this isn't a limitation! We can get something that looks and behaves like a multi-argument function by writing it in a curried style: you write a function which takes the first argument, and it returns _another_ function which takes the next argument, and when you have all your arguments in scope you can return the real result of the function.

[source,nix]
----
(x: (y: x + y))
----

You can drop those parentheses without changing how the expression is grouped, which makes curried functions easier to read and write.

[source,nix]
----
x: y: x + y
----

The `:` in a function definition is "right associative", so `x: y: x + y` means the same thing as `(x: (y: (x + y)))`.

To use a curried function, simply supply all the arguments.

[source,nix]
----
let
  add = (x: y: x + y);
in
  add 1 2
#-> 3
----

Function application is "left associative", so `add 1 2` means the same thing as `(((add) 1) 2)`.

NOTE: See <<_appendix_a_currying,Appendix A: Currying>> for more.

==== Named arguments

Functions can also destructure attribute set arguments.

[source,nix]
----
{x, y}: x + y
----

The above function expects a single set argument which has two elements: `x` and `y`. This is often used to pass named arguments to a function.

[source,nix]
----
let
  add = ({x, y}:
    x + y
  );
in
  add {x = 1; y = 2;}
#-> 3
----

Note that such a function requires an attribute set which has _exactly_ the keys used in the pattern. Missing or unexpected keys will cause an error.

[source,nix]
----
let
  hello = ({name}:
    "Hello ${name}"
  );
in
  hello { name = "world"; x = false; }
#-> error: ‘hello’ at (string):2:12 called with unexpected argument ‘x’, at (string):6:3
----

To allow extra keys to be ignored, you can mention `...`.

[source,nix]
----
let
  hello = ({name, ...}:
    "Hello ${name}"
  );
in
  hello { name = "world"; x = false; }
#-> "Hello world"
----

To allow missing keys, you can give them default values with `?`.

[source,nix]
----
let
  hello = ({name ? "world"}:
    "Hello ${name}"
  );
in
  hello {}
#-> "Hello world"
----

=== Paths

Where most languages treat file paths as simple strings, Nix has a special value type for paths. Roughly speaking, any word with `/` characters in it is parsed as a path literal.

[source,nix]
builtins.typeOf /etc/nixos/configuration.nix
#-> "path"

If you write a relative path, it will automatically be resolved to an absolute path relative to the Nix file it's written in (or relative to the current directory in `nix-repl` or `nix-instantiate`). Be sure to write it beginning with a `./` so it gets properly parsed as a path.

[options="header"]
|====
| ~/blog/file.nix
a|
[source,nix]
----
./.gitignore
#-> /Users/ryanartecona/blog/.gitignore
----
|====

This is similar to how you might manipulate paths starting from `__FILE__` in Ruby, `__file__` in Python, `__filename` in Node.js, etc., except you write the relative path as a literal, and Nix knows to resolve it to an absolute path automatically.

You can just as well use a path literal for directories, too. If referring to the directory of the current Nix file, be sure to spell it `./.` so it contains a slash.

[options="header"]
|====
| ~/blog/file.nix
a|
[source,nix]
----
./.
#-> /Users/ryanartecona/blog
----
|====

You can append a path to a base path with `+`.

[source,nix]
/path/to/project + /path/to/file.txt
#-> /path/to/project/path/to/file.txt

Just be sure the right-hand path is absolute, because if it's relative, it almost certainly won't be what you expect!

[source,nix]
/path/to/project + path/to/file.txt
#-> /path/to/project/Users/ryanartecona/blog/path/to/file.txt

So paths totally aren't strings. But if you ever need to, you can freely cast them back and forth.

[source,nix]
toString /path/to/file.txt
#-> "/path/to/file.txt"

[source,nix]
/. + "/path/to/file.txt"
#-> /path/to/file.txt

NOTE: There is also a `builtins.toPath` function to cast a string to a path, but it's link::https://github.com/NixOS/nix/issues/1074[currently broken]. In the meantime, this is a workaround.

Paths are useful for relative imports, which you will see next. Later on, you'll also see that they have special semantics -- different than strings! -- when used in derivations.

===== TODO: antiquoted paths in derivations (?)

===== TODO: `NIX_PATH` & `<path>`

[source,bash]
nix-instantiate -I mypath=./ --eval "<mypath>"

=== Imports

At any point in a Nix expression, you may import some other Nix expression from a different file.

[options="header"]
|====
| file.nix
a|
[source,nix]
{ hello = "world"; }
|====

[source,nix]
import ./file.nix
#-> { hello = "world"; }

Because Nix is expressions all the way down, importing is quite simple to explain: a Nix file (e.g. `file.nix`) contains a single well-formed Nix expression, and when you import that file with `import ./file.nix`, it's as if the `import ./file.nix` expression just gets replaced with the contents of `file.nix`.

For example, it's common to extract common library functions into their own file, and `import` them where needed. Your library, call it `lib.nix`, will just be a big attribute set of functions or whatever other values, and the importing files can use `.` access syntax to use what they need.

[options="header"]
|====
| lib.nix
a|
[source,nix]
{
  hello = (name:
    "Hello, ${name}!"
  );
}
|====

[source,nix]
let
  lib = import ./lib.nix;
in
  lib.hello "world"
#-> "Hello, world!"

Since `let` expressions are already available to create local bindings, you can use them to create private helpers that don't get exposed publicly.

[options="header"]
|====
| lib.nix
a|
[source,nix]
let
  greet = ({ pre, post}: name:
    pre + (toString name) + post
  );
  hello = greet { pre = "Hello, "; post = "!"; };
  goodbye = greet { pre = "Goodbye, "; post = "."; };
in
  { inherit hello goodbye; }
|====

[source,nix]
let
  lib = import ./lib.nix;
in
  [ (lib ? hello) (lib ? greet) ]
#-> [ true false ]

For Nix files which are libraries (attribute sets) of functions, it's a common pattern to import the file and immediately bring all its elements into scope using `with`.

[source,nix]
with import ./lib.nix;
hello "Nix"
#-> "Hello, Nix!"

== Evaluation

=== What is _lazy evaluation_?

Evaluation in Nix is _lazy_. This is important to understand, but if you're not already comfortable with a lazy language (e.g. Haskell), it may sound foreign and daunting. But don't worry! Though it's true that most languages aren't pervasively lazy, it's also true that lots of programming languages have _some_ lazy elements, so laziness is likely just a familiar concept by an unfamiliar name.

Take the following Javascript snippet.

[source,js]
----
function getTrue() {
  return true;
}
function throwError() {
  throw new Error('derp');
}

getTrue() || throwError()
----

Does the above snippet throw an error? No it doesn't! But why?

If you understand why, you know it's because `||` only evaluates its right-hand side if its left-hand side evaluates to `false` (or something falsy). If the left-hand side is `true` (or truthy), then the whole compound expression can evaluate to whatever was on the left-hand side _without_ evaluating the right-hand side at all. In the above example, the left-hand side is indeed `true`, so the right-hand side never gets evaluated, so we never see a thrown error.

This behavior is usually called "short-circuiting", especially in the context of boolean operators. Turns out, this is also lazy evaluation! You could just as accurately say `||` and `&&` are lazy operators.

So, of course, Nix's `||` and `&&` and other boolean operators are lazy, too.

[source,nix]
true || (null + null)
#-> true

We know this didn't evaluate the right-hand side, because if it did, we would've gotten an error.

[source,nix]
null + null
#-> error: cannot coerce null to a string, at (string):1:1

So what does it mean for the _whole language_ to be lazy? Well, in general, all expressions will avoid evaluating any of their subexpressions unless and until absolutely necessary.

A `let ... in ...` expression won't evaluate one of its bound values until that value's name is evaluated in the `in ...` body. We can prove this to ourselves by putting an _intentional bug_ in our program, and if we never see an error, that will mean the buggy expression never got evaluated.

[source,nix]
let
  bug = (null + null);
  foo = "bar";
in
  foo
#-> "bar"

An attribute set won't evaluate one of its values unless and until its corresponding key is accessed.

[source,nix]
let
  bug = (null + null);
  set = { foo = "bar"; ohnoes = bug; };
in
  set.foo
#-> "bar"

A list also won't evaluate its elements unless and until they are accessed.

[source,nix]
let
  bug = (null + null);
  list = [ true bug ];
in
  builtins.elemAt list 0
#-> true

An import of another Nix file won't actually go open the file unless and until it's evaluated.

[source,nix]
with import /a/path/to/some/file/that/does/not/exist.nix;
"coolio"
#-> "coolio"

Now, here's the extra weird one. Most programming languages will evaluate a functions' arguments _before_ calling a function with the supplied arguments. Nix doesn't. When you apply a function to an argument (or arguments, in curried style), that argument _may not_ have already been evaluated when the application evaluates. Moreover, if the function body avoids evaluating that argument, it may _remain_ unevaluated even after the function "returns" (or more precisely, after the application fully evaluates)!

Here's an example. If I write a function `const` which takes 2 curried arguments, and just gives back the first argument without evaluating the second argument, then I can give `const` a buggy expression as its second argument and _still_ never see a resulting error.

[source,nix]
let
  bug = (null + null);
  const = (x: y:
    x
  );
in
  const true bug
#-> true

In contrast, here's an example of a function `ifThenElse` that I've often wished I could write in Javascript.

[source,js]
----
function ifThenElse(condition, thenBranch, elseBranch) {
  if (condition) {
    return thenBranch;
  } else {
    return elseBranch;
  }
}
function throwError() {
  throw new Error('derp');
}

ifThenElse(true, "coolio", throwError());
#-> Uncaught Error: derp
----

Do you see why this doesn't work? The `throwError()` argument is evaluated before `ifThenElse` is even called, so our program derps out instead of just returning `"coolio"`. This is why you can't write a function that short-circuits quite the same as an `if` statement or a `&&` operator, because function arguments get evaluated before the function gets called. (You can work around this limitation in Javascript with anonymous functions and the like, but it's not quite the same.)

But in Nix, this would work!

[source,nix]
let
  ifThenElse = (condition: thenBranch: elseBranch:
    if condition then
      thenBranch
    else
      elseBranch
  );
  throwError = ({}:
    null + null
  );
in
  ifThenElse true "coolio" (throwError {})
#-> "coolio"

Coolio indeed. This works without throwing an error because Nix is a pervasively lazy language. _That's_ what it means for a language to be lazy.

=== Traces & debug logging
===== TODO: Traces
=== Errors
===== TODO: Errors (exceptions, assertions, aborts)

== Standard library

The Nix standard library has two parts, practically speaking.

Nix itself ships with some primitives called `builtins`. These are all you have available if you use a plain `nix-repl` or `nix-instantiate`. Nix has no foreign function interface (FFI), so everything written in Nix is limited to what the `builtins` allow you to express. For example, it's not possible to interactively prompt a user for confirmation inside Nix, because nothing in `builtins` lets you read from `stdin` (or any other file descriptor), and there's no facility in Nix for adding that capability via a library.

The `nixpkgs` package set ships with its own library of functions and helpers in `<nixpkgs/lib>` (often seen in Nix code as `pkgs.lib`, `stdenv.lib`, or just `lib`). Some of what's there is geared specifically toward writing Nix derivations or working with NixOS configuration modules, but most of it is very generic. Everything there is built on top of `builtins` as a foundation (there's no other way!), but `<nixpkgs/lib>` often provides better ergonomics. `nixpkgs` itself is ubiquitous in the Nix ecosystem, so since `<nixpkgs/lib>` is available anywhere `nixpkgs` is, it's a sort of _de facto_ standard library.

=== builtins
===== TODO: builtins
=== `nixpkgs/lib`
===== TODO: nixpkgs/lib

== Appendix

=== #Appendix A: Currying#
If you're unfamiliar with currying, here's how a curried `add` function could be written and used in Javascript.

[source,javascript]
----
const add = (x) => {return ((y) => {return x + y})};
add(1);
//-> (y) => {return x + y}
add(1)(2);
//-> 3
const add1 = add(1);
add1(2);
//-> 3
----

If you'd like to read up more on currying, introductory blog posts abound. You can google for one in your language of choice. If you understand what's going on above, though, that's really all there is to it.
